import groovy.json.JsonSlurper

buildscript {
    ext.kotlin_version = '1.2.51'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha08'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "$rootDir/../../node_modules/react-native/android"
        }
        maven { url "https://jitpack.io" }
        maven { url "$rootDir/../../node_modules/jsc-android/dist" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// @see https://medium.com/@andr3wjack/versioning-react-native-apps-407469707661
static def getNpmVersion() {
    def inputFile = new File("../../package.json")
    def packageJson = new JsonSlurper().parseText(inputFile.text)
    return packageJson["version"]
}

static def getNpmVersionArray() { // major [0], minor [1], patch [2]
    def (major, minor, patch) = getNpmVersion().tokenize('.')
    return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(patch)] as int[]
}

subprojects {
    ext {
        def npmVersion = getNpmVersionArray()
        versionMajor = npmVersion[0]
        versionMinor = npmVersion[1]
        versionPatch = npmVersion[2]
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion 28
                buildToolsVersion '28.0.2'
            }
        }
    }
}
